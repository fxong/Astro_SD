;Purpose: derive the average spectrum of a datacube
;Usage: averspec, fitsfile, v=[v1, v2], rmsfile=rmsfile, fac=n 
;Input:
;  fitsfile: the original fitsfile WITHOUT suffix
;Input keyword:
;  v: set the velocity range of averspec, the default is the original velocity range
;  rmsfile: the rms fitsfile used to set the weight of datacube values, 
;             the default weight is 1
;  fac: set the factor which times the spectrum
;Caution:
;  the unit of v1 and v2 is km/s
;History:
;  Completed. 22 Nov 2015. 
;  Bug fixed. 15 Dec 2015.
;  Add Gaussian fit. 26 Jan 2016.
;Copyright: written by fxong@PMO

function coord2pix, hdr, value, dim, ds9pix=ds9pix
value=float(value)
dim=strcompress(string(dim),/remove_all)
pix=(value-sxpar(hdr,'CRVAL'+dim))/sxpar(hdr,'CDELT'+dim)+sxpar(hdr,'CRPIX'+dim)
if keyword_set(ds9pix) then return,pix else return,pix-1
end

pro clipvel, olddat, oldhdr, newdat, newhdr, vel1, vel2
;clip the datacube with the given velocity range
  x1=0
  num1=sxpar(oldhdr,'NAXIS1')
  y1=0
  num2=sxpar(oldhdr,'NAXIS2')
  z1=round(coord2pix(oldhdr,vel1,3))
  z2=round(coord2pix(oldhdr,vel2,3))
  num3=z2-z1+1
  newdat=dblarr(num1,num2,num3)
  for i=0,num1-1 do begin
    for j=0,num2-1 do begin
      for k=0,num3-1 do begin
        newdat[i,j,k]=olddat[i+x1,j+y1,k+z1]
      endfor
    endfor
  endfor

  newhdr=oldhdr
  sxaddpar,newhdr,'NAXIS3',num3
  sxaddpar,newhdr,'CRVAL3',vel1
  sxaddpar,newhdr,'CRPIX3',1
end

pro c2v,hdr,c,v
        if n_params() lt 3 then begin
                print, 'Syntax - C2V, hdr, c, v'
                return
        endif
        nc = sxpar(hdr,'NAXIS3')
        v0 = sxpar(hdr,'CRVAL3')
        c0 = sxpar(hdr,'CRPIX3')
        dv = sxpar(hdr,'CDELT3')
        v = (c-c0+1)*dv+v0
end

;Main Procedure
pro averspec, fitsfile, v=v, rmsfile=rmsfile, fac=fac
;average spectra of a cube
if n_params() lt 1 then begin
  print,'Syntax - AVERSPEC, fitsfile, [v= , rmsfile= , fac= ]'
  return
endif
if ~file_test(fitsfile+'.fits') then begin
  print,'Error: Fits file does not exist! Input fits file without suffix.'
  return
endif
if keyword_set(rmsfile) then begin
  if ~file_test(rmsfile+'.fits') then begin
    print,'Error: Rms file does not exist! Input rms file without suffix.'
    return
  endif
endif

if keyword_set(v) then begin
  print,'Run the clipvel'
  fits_read,fitsfile+'.fits',olddat,oldhdr
  factor=abs(sxpar(oldhdr,'CDELT3') lt 100)?(1d):(1000d)
  clipvel, olddat, oldhdr, dat, hdr, v[0]*factor, v[1]*factor
endif else begin
  fits_read,fitsfile+'.fits',dat,hdr
endelse

if keyword_set(rmsfile) then begin
  fits_read,rmsfile+'.fits',rdat,rhdr
  wei=1/rdat^2
endif else begin
  wei=1
endelse

nc=sxpar(hdr,'NAXIS3')
c=findgen(nc)
c2v,hdr,c,v
factor=abs(sxpar(hdr,'CDELT3') lt 100)?(1d):(1000d)
velo=v/factor

spec=dblarr(nc)
for i=0, nc-1 do begin
  tempdat=dat[*,*,i]
  temptotval=total(tempdat*wei, /nan)
  tempdat[where(tempdat eq 0)]=!VALUES.F_NAN
  temptotwei=total(finite(tempdat)*wei, /nan)
  spec[i]=temptotval/temptotwei
endfor

fit=GAUSSFIT(velo,spec,coeff,nterms=4,sigma=fsigma)
velosm=congrid(velo,5*n_elements(velo),/interp)
specsm=coeff[0]*exp(-((velosm-coeff[1])/coeff[2])^2/2)+coeff[3];+coeff[4]*velosm;+coeff[5]*(velosm)^2
DeltaV=2*sqrt(2*alog(2))*coeff[2]

openw,lun,fitsfile+'.averspec',/get_lun
printf,lun,'#    Velocity(km/s)    Tmb(k)'
for i=0, nc-1 do printf,lun,velo[i],spec[i]
printf,lun,'#    line width:',DeltaV,' km/s'
free_lun,lun

if keyword_set(fac) then spec=spec*fac
cgplot,velo,spec,psym=10,color='blue',thick=2.5,$
     xtitle='!6Velocity (km/s)', ytitle='!6T!Dmb!N (K)',charsize=1.2,charthick=1
if keyword_set(fac) then begin
  cgtext,0.15,0.85,'!6Factor=',charsize=1.2,charthick=1,/normal
  cgtext,0.175,0.85,string(fac),charsize=1.2,charthick=1,/normal
endif
;cgplot,velosm,specsm,psym=0,color='red',thick=1.5,/overplot

end

;averplot.pro

;Purpose: plot the average spectrum of a datacube
;Usage: averplot, datst, datnd=datnd, datrd=datrd, facst=n, facnd=n, facrd=n
;Input:
;  datst: the data file generated by averspec.pro without suffix
;Input keyword:
;  datnd: set the second data file without suffix, if provided
;  datrd: set the third data file without suffix, if provided
;  facst: set the first factor which times the spectrum of datst
;  facnd: set the second factor which times the spectrum of datnd
;  facrd: set the third factor which times the spectrum of datrd
;Caution:
;  this is a semi-automatic procedure, the flagged parameters need to be set by hand
;Copyright: written by fxong@PMO

pro averplot, datst, datnd=datnd, datrd=datrd, facst=facst, facnd=facnd, facrd=facrd

if n_params() lt 1 then begin
  print,'Syntax - AVERPLOT, datst, [datnd= , datrd= , facst= , facnd= , facrd= ]'
  return
endif

readcol,datst+'.averspec',f='D,D',velost,specst
vmin=min(velost)
vmax=max(velost) 
if keyword_set(facst) then specst=specst*facst

if keyword_set(datnd) then begin
  readcol,datnd+'.averspec',f='D,D',velond,specnd
  vmintemp=min(velond) & vmin=max([vmin,vmintemp])
  vmaxtemp=max(velond) & vmax=min([vmax,vmaxtemp])
  if keyword_set(facnd) then specnd=specnd*facnd
endif

if keyword_set(datrd) then begin  
  readcol,datrd+'.averspec',f='D,D',velord,specrd
  vmintemp=min(velord) & vmin=max([vmin,vmintemp])
  vmaxtemp=max(velord) & vmax=min([vmax,vmaxtemp])
  if keyword_set(facrd) then specrd=specrd*facrd
endif

set_plot,'ps'
device,filename='averspec.eps',/encapsulated,decomposed=1;,xsize=,ysize=

cgplot,velost,specst,psym=10,color='blue',thick=2.5,$
       xrange=[vmin,vmax],$;xtickinterval=,xminor=,xtickformat='(f0.1)',xthick=1,$
       ;yrange=[,],ytickinterval=,yminor=,ytickformat='(f0.1)',ythick=1,$
       xtitle='!6Velocity (km/s)', ytitle='!6T!Dmb!N (K)',charsize=1.4,charthick=1
;cgplot,[,],[,],psym=0,color='blue',thick=3,/overplot
;cgtext,,,'!6',charsize=1.4,charthick=1,color='blue';!U12!NCO !MX!X

if keyword_set(datnd) then begin
  cgplot,velond,specnd,psym=10,color='orange',thick=2.5,/overplot
  ;cgplot,[,],[,],psym=0,color='orange',thick=3,/overplot
  ;cgtext,,,'!6',charsize=1.4,charthick=1,color='orange';!U13!NCO !MX!X
endif

if keyword_set(datrd) then begin
  cgplot,velord,specrd,psym=10,color='red',thick=2.5,/overplot
  ;cgplot,[,],[,],psym=0,color='red',thick=3,/overplot
  ;cgtext,,,'!6',charsize=1.4,charthick=1,color='red';C!U18!NO !MX!X
endif

device,/close_file
set_plot,'x

end
